%options
  "--incremental"
  "--default-constructor-quantities"
  "--prune-clauses"

-- The prelude

data N : Type {
  Z : N,
  S : N -> N
}

data B : Type {
  T : B,
  F : B
}

data List : Type {
  Cons : B -> List -> List,
  Nil : List
}

data Maybe : Type -> Type {
  Nothing : forall a. Maybe a,
  Just : forall a. a -> Maybe a
}

not : B -> B {
  not T ~> F,
  not F ~> T
}


-- The reverse view

data Id : forall a. a -> a -> Type {
  Refl : forall a (x :I a). Id a x x
}

id : forall a. a -> a {
  id ~> \a. \x. x
}

subst : forall a (P : (x :L a) -> Type) (x : a) (y : a).
  Id a x y -> P x -> P y
{
  forall a (P : _ -> _) x.
  subst a P x [x] ({Refl} [a] [x]) ~> \w. w
}

cong : forall a b (f : (x :L a) -> b) (x : a) (y : a).
  Id a x y -> Id b (f x) (f y)
{
  forall a b (f : _ -> _) x.
  cong a b f x [x] [Refl a x] ~> Refl b (f x)
}

one : (x : B) -> List {
  forall x.
  one x ~> Cons x Nil
}

app : List -> List -> List {
  forall ys.
  app Nil ys ~> ys,

  forall x xs ys.
  app (Cons x xs) ys ~> Cons x (app xs ys)
}

appRightNeutral : forall ys. Id List ys (app ys Nil) {
  appRightNeutral Nil ~> Refl List Nil,

  forall x xs.
  appRightNeutral (Cons x xs) ~>
    cong List List (Cons x) xs (app xs Nil) (appRightNeutral xs)
}

appAssoc : forall xs ys zs.
  Id List (app (app xs ys) zs) (app xs (app ys zs))
{
  forall ys zs.
  appAssoc Nil ys zs ~> Refl List (app ys zs),

  forall x xs ys zs.
  appAssoc (Cons x xs) ys zs ~>
    cong List List (Cons x) (app (app xs ys) zs) (app xs (app ys zs)) (appAssoc xs ys zs)
}

data Rev : List -> Type {
  RNil : Rev Nil,
  RSnoc : forall xs.
    (x : B) -> (rxs : Rev xs) -> Rev (app xs (one x))
}

rev' : forall xs. (rxs : Rev xs) -> (ys : List) -> Rev (app xs ys) {
  forall xs rxs.
  rev' xs rxs Nil ~> subst List Rev xs (app xs Nil) (appRightNeutral xs) rxs,

  forall xs rxs y ys.
  rev' xs rxs (Cons y ys)
    ~> subst List Rev
          (app (app xs (one y)) ys)
          (app xs (Cons y ys))
          (appAssoc xs (one y) ys)
          (rev' (app xs (one y)) (RSnoc xs y rxs) ys)
}

rev : (xs : List) -> Rev xs {
  forall xs.
  rev xs ~> rev' Nil RNil xs
}

reverse' : forall xs. (rxs : Rev xs) -> List {
  reverse' [Nil] RNil ~> Nil,

  forall xs x rxs.
  reverse' [app xs (one x)] (RSnoc xs x rxs) ~> Cons x (reverse' xs rxs)
}

reverse : List -> List {
  forall (xs : List).
  reverse xs ~> reverse' xs (rev xs)
}

-- Palindrome library

data V : List -> Type {
  VNil : V Nil,
  VOne : (x : B) -> V (Cons x Nil),
  VTwo : (x : B) -> forall xs -> (u : V xs) -> (y : B)
    -> V (Cons x (app xs (one y)))
}

length : List -> N {
  length Nil ~> Z,

  forall x xs.
  length (Cons x xs) ~> S (length xs)
}

index : N -> List -> List -> List {
  forall xs ys.
  index       Z         xs        ys  ~> Nil,

  forall x xs y ys.
  index    (S Z)  (Cons x xs) (Cons y ys) ~> Cons x Nil,

  forall n x xs y ys.
  index (S (S n)) (Cons x xs) (Cons y ys) ~> Cons x (app (index n xs ys) (one y)),

  forall n.
  index    (S n)        Nil        Nil  ~> Nil
}

build : (n : N) -> (xs : List) -> (ys : List) -> V (index n xs ys) {
  forall xs ys.
  build       Z         xs        ys  ~> VNil,

  forall x xs y ys.
  build    (S Z)  (Cons x xs) (Cons y ys) ~> VOne x,

  forall n x xs y ys.
  build (S (S n)) (Cons x xs) (Cons y ys) ~> VTwo x (index n xs ys) (build n xs ys) y,

  forall n.
  build    (S n)        Nil        Nil  ~> VNil
}

postulate eq : (xs : List) -> Id List (index (length xs) xs (reverse xs)) xs.

toV : (xs : List) -> V xs {
  forall xs.
  toV xs
    ~> subst List V
        (index (length xs) xs (reverse xs))
        xs
        (eq xs)
        (build (length xs) xs (reverse xs))
}

data IsPalindrome : List -> Type {
  PNil : IsPalindrome Nil,
  POne : (b : B) -> IsPalindrome (Cons b Nil),
  PTwo : (b : B) -> forall xs -> (pf : IsPalindrome xs) -> IsPalindrome (Cons b (app xs (one b)))
}

decEq : (x : B) -> (y : B) -> Maybe (Id B x y) {
  decEq T T ~> Just (Id B T T) (Refl B T),
  decEq F F ~> Just (Id B F F) (Refl B F),
  decEq T F ~> Nothing (Id B T F),
  decEq F T ~> Nothing (Id B F T)
}

isPalinV' : (x : B) -> (y : B) -> (xs :I List) -> (v : V xs)
    -> (pfB : Maybe (Id B x y))
    -> (pfV : Maybe (IsPalindrome xs))
    -> Maybe (IsPalindrome (Cons x (app xs (one y))))
{
  forall x xs v pfV.
  isPalinV' x [x] xs v
    (Just [Id B x x] [Refl B x])
    (Just [IsPalindrome xs] pfV)
    ~> Just (IsPalindrome (Cons x (app xs (one x)))) (PTwo x xs pfV),

  forall x y xs v pfB pfV.
  isPalinV' x y xs v pfB pfV
    ~> Nothing (IsPalindrome (Cons x (app xs (one y))))
}

isPalinV : (xs : List) -> (v : V xs) -> Maybe (IsPalindrome xs) {
  isPalinV [Nil]        VNil     ~> Just (IsPalindrome Nil) PNil,

  forall x.
  isPalinV [Cons x Nil] (VOne x) ~> Just (IsPalindrome (Cons x Nil)) (POne x),

  forall x xs y v.
  isPalinV [Cons x (app xs (one y))] (VTwo x xs v y)
    ~> isPalinV' x y xs v (decEq x y) (isPalinV xs v)
}

isPalindrome : (xs : List) -> Maybe (IsPalindrome xs) {
  forall xs.
  isPalindrome xs ~> isPalinV xs (toV xs)
}

-- Main Palindrome program

genList : B -> N -> List {
  forall b.
  genList b Z ~> Nil,

  forall b n.
  genList b (S n) ~> Cons b (genList (not b) n)
}

isJust : forall a. (x : Maybe a) -> B {
  forall a x.
  isJust a (Just [a] x) ~> T,

  forall a.
  isJust a (Nothing [a]) ~> F
}

testList : List
  ~> Cons T (Cons F (Cons F (Cons T (Cons F (Cons F (Cons T Nil)))))).

main : Maybe (IsPalindrome testList)
  ~> isPalindrome testList.
